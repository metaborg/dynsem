module dynsem/Signatures

imports
  dynsem/Common
  ds

context-free syntax

  ModuleSection.Signatures = <
    signature
        <{SignatureSection "\n"}*>>

context-free syntax // sort declarations

  SignatureSection.Sorts = <
    sorts
      <{SortDecl "\n"}*>>
  
  SortDecl.SortDecl = <<ID>>
  
context-free syntax // variable schemes
  
  SignatureSection.VariableSchemes = <
    variables
      <{VariableScheme "\n"}*>>
  
  VariableScheme.VariableScheme = [[ID] : [Type]]

context-free syntax // variable schemes
  
  SignatureSection.Aliases = <
    sort aliases
      <{AliasDecl "\n"}*>>
  
  AliasDecl.AliasDecl = [[ID] = [Type]]

context-free syntax // semantic components

  SignatureSection.Components = <
    components
      <{CompDecl "\n"}*>>
  
  CompDecl.CompDecl = [[ID] : [Type]]
  CompDecl.CompDeclDefault = [[ID] : [Type] = [Term]]

context-free syntax // constructor declarations

  SignatureSection.Constructors = <
    constructors
      <{ConsDecl "\n"}*>>

  ConsDecl.NullaryConsDecl = [[ID] : [Type] [ConsAnnos]]
  ConsDecl.ConsDecl = [[ID] : [{Type " * "}+] -> [Type] [ConsAnnos]]
  
  ConsAnnos.NoAnnos = []

  ConsAnnos.Annos = [{[ { ConsAnno "," }* ]}]
  ConsAnno.ImplicitAnno = [implicit]
  ConsAnno.MetafunAnno = [meta-function]

  Type.SimpleSort = <<ID>>
  Type.ListSort = <List(<Type>)> 
  Type.TupleSort = [([{Type " * "}+])]
  Type = MapType
    
  MapType.MapSort = [Map([Type],[Type])]

template options

  tokenize: "<("
  
context-free syntax // native data types

  SignatureSection.NativeDataTypes = <
    native datatypes
      <{NativeTypeDecl "\n"}*>>
  
  JSNIPPET = <<STRING>>
  
  NativeTypeDecl.NativeBaseTypeDecl =
    [[JSNIPPET] as [ID] { [{NativeFunctionDecl "\n"}*] } ]
  
  NativeTypeDecl.NativeSubTypeDecl =
    [[JSNIPPET] as [ID] -> [ID] { [{NativeFunctionDecl "\n"}*] } ]
  
  NativeFunctionDecl.NativeFunctionDecl =
    [[ID] : [{Type " * "}*] -> [Type]] 
  
  NativeFunctionDecl.NativeNoArgFunctionDecl =
    [[ID] : [Type]]

context-free syntax // native operator declarations

  SignatureSection.NativeOperators = <
    native operators
      <{NativeOpDecl "\n"}*>>
  
  NativeOpDecl.NativeOpDecl = [[ID] : [{Type " * "}+] -> [Type]]
  NativeOpDecl.NullaryNativeOpDecl = [[ID] : [Type]]

context-free syntax // internal constructor declarations

  SignatureSection.NativeConstructors = <
    native constructors
      <{NativeConsDecl "\n"}*>>

  NativeConsDecl.NativeConsDecl = [[ID] : [{Type " * "}+] -> [Type]]
  
  NativeConsDecl.NullaryNativeConsDecl = [[ID] : [Type]]

context-free syntax
  
  SignatureSection.ArrowDeclarations = <
    arrows
      <{DeclaredArrow "\n"}*>>
  
  DeclaredArrow = ArrowDeclaration
  
  ArrowDeclaration.DefaultArrowDecl = [[ArrowROs] [Type] [ArrowRWs] --> [Type] [ArrowRWs]]

  ArrowDeclaration.ArrowDecl = [[ArrowROs] [Type] [ArrowRWs] -[IDNOHYPH]-> [Type] [ArrowRWs]]
  
  ArrowDeclaration.MetaFunDecl = [[ArrowROs] [ID]([{Type ", "}*]) [ArrowRWs] --> [Type] [ArrowRWs]]
  
  ArrowROs.NoArrowROs = []
  ArrowROs.ArrowROs = [[{Label ", "}+] |-]
  
  ArrowRWs.NoArrowRWs = []
  ArrowRWs.ArrowRWs = [:: [{Label ", "}+]]
   
    

