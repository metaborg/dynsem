module backend/interpreter/sorts/terms

imports
  signatures/-
  signatures/dynsem/-
  backend/interpreter/-
  backend/interpreter/sorts/util
  backend/utils/-
  backend/common/-
  libjava-front

rules

  ds-to-interp-terms-sortdecls(|cons-dec*) = 
    filter(is-generatable-sortdecl; require(ds-to-interp-terms-sortdecl(|cons-dec*), debug-decl-name|"Term generation failed for sort"))

  ds-to-interp-terms-sortdecl(|cons-dec*):
    dec@SortDecl(s, _) ->
      compilation-unit |[
        package ~x:<get-opt> TermPkg();
        
        import org.spoofax.interpreter.core.Tools;
        import org.metaborg.meta.lang.dynsem.interpreter.terms.IApplTerm;
        import com.oracle.truffle.api.CompilerDirectives.TruffleBoundary;
        import org.spoofax.interpreter.terms.*;
        import com.oracle.truffle.api.CompilerAsserts;
        import org.metaborg.meta.lang.dynsem.interpreter.terms.concrete.ApplTerm;
        
        public abstract class x_class {
        
          ~mcreate0*
        
        }
      ]|
    where
      x_class := <jclass-term> dec;
      appl-cons-dec* := <filter(?ConsDecl(_, _, SimpleSort(s),_))> cons-dec*; 
      if <is-value-sort> s
      then
        mcreate0* := [
          class-body-dec |[
              @TruffleBoundary
              public static ApplTerm create(IStrategoTerm term) {
                throw new IllegalStateException("Value sorts cannot be created from Stratego terms");
              }
          ]| ]
      else
        bstm0* := <map(ds-to-interp-terms-sortdec-createdisp)> appl-cons-dec*;
        impl-consdec* := <filter(?ConsDecl(_, [_], SimpleSort(s), Annos([ImplicitAnno()])))> cons-dec*;
        bstm1 := <foldr(! bstm |[ throw new IllegalStateException("Unsupported term: " + term); ]|, ds-to-interp-terms-sortdec-impldisp)> impl-consdec*;
        mcreate0* := [
          class-body-dec |[
              @TruffleBoundary
              public static ApplTerm create(IStrategoTerm term) {
                CompilerAsserts.neverPartOfCompilation();
                assert term != null;
                bstm0*
                bstm1
              }
          ]| ]
      end
      

  ds-to-interp-terms-sortdec-createdisp:
    dec@ConsDecl(name, kid*, _, _) ->
      bstm |[
        if (Tools.isTermAppl(term) && Tools.hasConstructor((IStrategoAppl) term, ~e:Lit(String([Chars(name)])), ~i:<length; int-to-string> kid*)) {
          return ~x:<jclass-term> dec.create(term);
        }
      ]| 

  ds-to-interp-terms-sortdec-impldisp:
    (dec@ConsDecl(name, [sref@SimpleSort(_)], _, _), bstm0) ->
      bstm |[
        try {
          return new ApplTerm(~e:Lit(String([Chars(sort-string)])), ~e:Lit(String([Chars(name)])), new Object[] { e_create }, (~e:Lit(String([Chars(name)])) + "/1").intern());
        } catch (IllegalStateException ~x:<newname>) {
          bstm0
        }
      ]|
    where
      x_class := <jclass-term> dec;
      e_create := <ds-to-interp-terms-sortdec-impldisp-constr> sref;
      sort-string := <string-type> sref
  
  ds-to-interp-terms-sortdec-impldisp-constr:
    sref@SimpleSort(s) -> e |[ ~x:<jclass-term> sref.create(term) ]|
    where
      <not(type-is-builtin)> s

  ds-to-interp-terms-sortdec-impldisp-constr:
    sref@SimpleSort("String") -> e |[ ((IStrategoString) term).stringValue() ]|

  ds-to-interp-terms-sortdec-impldisp-constr:
    sref@SimpleSort("Int") -> e |[ ((IStrategoInt) term).intValue() ]|

rules // creation

  ds-to-interp-sorts-to-create-arg:
    (e_term, sref@SimpleSort(s)) -> e |[ ~x:<jclass-term> sref.create(e_term) ]|
    where
      <not(type-is-builtin)> s;
      s-def := <lookup-def(|Types())> s;
      <lookup-prop(|SortKind())> s-def => LanguageSort();
      <not(has-prop(|ValueSort()))> s-def
  
  ds-to-interp-sorts-to-create-arg:
    (e_term, sref@SimpleSort(s)) -> e |[ null ]|
    where
      <not(type-is-builtin)> s;
      s-def := <lookup-def(|Types())> s;
      <lookup-prop(|SortKind())> s-def => LanguageSort();
      <has-prop(|ValueSort())> s-def



