module backend/interpreter/utils

imports
  libspoofax/resource/path
  lib-ds
  backend/utils/utils

signature
  sorts
    DSOpt
 
  constructors
    FilePath : DSOpt
    ProjectPath : DSOpt
    LangName : DSOpt
    TableSource : DSOpt
    LangVersion : DSOpt
    MimeType : DSOpt
    StartSymbol : DSOpt
    StartConstrName : DSOpt
    StartConstrArity : DSOpt
    GenProject : DSOpt
    CreateProject : DSOpt
    CleanProject : DSOpt
    EnableBacktracking : DSOpt
    EnableSafeComponents : DSOpt
    EnableTermCaching : DSOpt
    MavenGroupId : DSOpt
    MavenArtifactId : DSOpt
    JavaGenDirRel : DSOpt
    JavaGenDirAbs : DSOpt
    TopPkg : DSOpt
    TermPkg : DSOpt
    MatchPkg : DSOpt
    BuildPkg : DSOpt
    NativePkg : DSOpt
    RuleRegistry : DSOpt
    PreProcessor : DSOpt
    TargetSpecRel : DSOpt
    TargetSpecAbs : DSOpt
    TargetTableRel : DSOpt
    TargetTableAbs : DSOpt
    JVMArgs : DSOpt

rules

  get-opt(|prop) = <DSOpt> prop
  
  get-opt:
    prop -> <DSOpt> prop

  ds-to-interp-init-options(|path, project-path) =
  with(
    <ds-to-interp-get-options> (path, project-path);
    filter(\ (opt,val) -> <rules(DSOpt:+ opt -> val)> where not("false" := val) \)
  )
 
  ds-to-interp-get-options:
    (path, project-path) -> [
      (FilePath(), path),
      (ProjectPath(), project-path),
      (LangName(), langname),
      (TableSource(), tbl-source),
      (LangVersion(), lang-ver),
      (MimeType(), lang-mime),
      (StartSymbol(), start-symb),
      (StartConstrName(), initcon-name),
      (StartConstrArity(), initcon-arity),
      (GenProject(), genproject-path),
      (CreateProject(), do-create-genproject),
      (CleanProject(), do-clean-genproject),
      (EnableBacktracking(), do-enable-backtracking),
      (EnableSafeComponents(), do-safe-components),
      (EnableTermCaching(), do-term-caching),
      (MavenGroupId(), genproject-groupid),
      (MavenArtifactId(), genproject-artifactid),
      (JavaGenDirRel(), java-gendir-rel),
      (JavaGenDirAbs(), java-gendir-abs),
      (TopPkg(), gen-pkg),
      (TermPkg(), terms-pkg),
      (BuildPkg(), tbuild-pkg),
      (MatchPkg(), tmatch-pkg),
      (NativePkg(), native-pkg),
      (RuleRegistry(), rulereg-class),
      (PreProcessor(), preprocess-class),
      (TargetSpecRel(), target-specfile-rel),
      (TargetSpecAbs(), target-specfile-abs),
      (TargetTableRel(), target-tblfile-rel),
      (TargetTableAbs(), target-tblfile-abs),
      (JVMArgs(), jvm-args)
    ]
    where
      local-project-path := <local-path> project-path;
      prop-file := $[[project-path]/dynsem.properties];
      langname := <ds-to-interp-read-prop-required(|"source.langname"); ds-utils-java-escape-id> prop-file;
      tbl-source := <read-property(|"source.table", "target/metaborg/sdf.tbl"); absolute-path(|local-project-path)> prop-file;
      lang-ver := <ds-to-interp-read-prop-required(|"source.version")> prop-file;
      lang-mime := <read-property(|"source.mimetype", $[application/x-[langname]])> prop-file;
      start-symb := <ds-to-interp-read-prop-required(|"source.startsymbol")> prop-file;
      initcon-name := <ds-to-interp-read-prop-required(|"source.initconstructor.name")> prop-file;
      initcon-arity := <ds-to-interp-read-prop-required(|"source.initconstructor.arity")> prop-file;
      genproject-path := <ds-to-interp-read-prop-required(|"project.path"); absolute-path(|local-project-path)> prop-file;
      do-create-genproject := <read-property(|"project.create", "false")> prop-file;
      do-clean-genproject := <read-property(|"project.clean", "false")> prop-file;
      do-enable-backtracking := <read-property(|"interpreter.fullbacktracking", "false")> prop-file;
      do-safe-components := <read-property(|"interpreter.safecomponents", "false")> prop-file;
      do-term-caching := <read-property(|"interpreter.termcaching", "false")> prop-file;
      genproject-groupid := <ds-to-interp-read-prop-required(|"project.groupid")> prop-file;
      genproject-artifactid := <ds-to-interp-read-prop-required(|"project.artifactid")> prop-file;
      java-gendir-rel := <read-property(|"project.javapath", "src/main/java")> prop-file;
      java-gendir-abs := $[[genproject-path]/[java-gendir-rel]];
      gen-pkg := <read-property(|"project.javapackage", $[[<ds-utils-java-escape-id> genproject-groupid].[<ds-utils-java-escape-id> genproject-artifactid].generated])> prop-file;
      terms-pkg := $[[gen-pkg].terms];
      tbuild-pkg := $[[terms-pkg].build];
      tmatch-pkg := $[[terms-pkg].match];
      native-pkg := <read-property(|"project.nativepackage", $[[langname].interpreter.natives])> prop-file;
      rulereg-class := <read-property(|"project.ruleregistry", "org.metaborg.meta.lang.dynsem.interpreter.nodes.rules.RuleRegistry")> prop-file;
      preprocess-class := <read-property(|"project.preprocessor", "org.metaborg.meta.lang.dynsem.interpreter.terms.ITermTransformer.IDENTITY")> prop-file;
      target-specfile-rel := <read-property(|"project.specpath", "src/main/resources/specification.aterm")> prop-file;
      target-specfile-abs := $[[genproject-path]/[target-specfile-rel]];
      target-tblfile-rel := <read-property(|"project.tablepath", "src/main/resources/parsetable.tbl")> prop-file;
      target-tblfile-abs := $[[genproject-path]/[target-tblfile-rel]];
      jvm-args := <read-property(|"interpreter.vmargs", "")> prop-file

  ds-to-interp-read-prop-required(|prop) =
    read-property(|prop, "N/A"); not(?"N/A" + ?"")
    <+ with(fail|$[Cannot read [prop] property])

  ds-to-interp-write-classes(|package) = ds-utils-java-emit-classes(id | package, <get-opt> JavaGenDirAbs())
