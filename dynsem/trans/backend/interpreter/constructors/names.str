module backend/interpreter/constructors/names

imports
  analysis/-
  signatures/-
  signatures/dynsem/-
  backend/interpreter/names
  backend/utils/utils
  backend/common/-
    
strategies // classes for sorts
  
  jclass-term:
    ConsDecl(name, kid*, _, _) -> <jclass-for-cons> (name, <length> kid*)
  
  jclass-term:
    Con(c, k*) -> <jclass-for-cons> (c, <length> k*)
  
  jclass-for-cons:
    (name, arity) -> classname
    where
      if jclass := <lookup-def(|Constructors()); lookup-prop(|JClass()); unquote(?'"')> (name, arity)
      then
        classname := jclass
      else
        classname := $[[<ds-utils-java-escape-id> name]_[arity]]
      end
    
  jclass-term-qual:
    ConsDecl(name, kid*, _, _) -> <jclass-for-cons-qual> (name, <length> kid*)
  
  jclass-term-qual:
    Con(c, k*) -> <jclass-for-cons-qual> (c, <length> k*)
  
  jclass-for-cons-qual:
    (name, arity) -> classname
    where
      if <lookup-def(|Constructors()); has-prop(|JClass())> (name, arity)
      then
        classname := <jclass-for-cons> (name, arity)
      else
        classname := $[[<get-opt> TermPkg()].[arity]]
      end
  
strategies // builder for sorts

  jclass-termbuilder:
    dec@ConsDecl(name, kid*, _, _) -> classname
    where
      if jclass := <lookup-def(|Constructors()); lookup-prop(|JClass()); unquote(?'"')> (name, <length> kid*)
      then
        classname := $[[jclass].Build]
      else
        classname := $[[<ds-utils-java-escape-id> name]_[<length> kid*]_B]
      end

  jclass-termbuilder-ng:
    dec@ConsDecl(name, kid*, _, _) -> classname
    where
      if jclass := <lookup-def(|Constructors()); lookup-prop(|JClass()); unquote(?'"')> (name, <length> kid*)
      then
        classname := $[[<pkg-of> jclass].[<jclass-term; classname-of> dec]Factory.BuildNodeGen]
      else
        classname := $[[<jclass-termbuilder> dec]NodeGen]
      end
  
strategies // checked builders
  
  jclass-checkedbuilder = fail

strategies // matcher for sorts
  
  jclass-matcher:
    dec@ConsDecl(name, kid*, _, _) -> classname
    where
      if jclass := <lookup-def(|Constructors()); lookup-prop(|JClass()); unquote(?'"')> (name, <length> kid*)
      then
        classname := $[[jclass].Match]
      else
        classname := $[[<ds-utils-java-escape-id> name]_[<length> kid*]_M]
      end
  
  jclass-matcher-ng:
    dec@ConsDecl(name, kid*, _, _) -> classname
    where
      if jclass := <lookup-def(|Constructors()); lookup-prop(|JClass()); unquote(?'"')> (name, <length> kid*)
      then
        classname := $[[<pkg-of> jclass].[<jclass-term; classname-of> dec]Factory.MatchNodeGen]
      else
        classname := $[[<jclass-matcher> dec]NodeGen]
      end
  
