module backend/interpreter/constructors/terms

imports
  signatures/-
  signatures/dynsem/-
  backend/common/-
  backend/interpreter/-
  backend/interpreter/constructors/util

strategies

  ds-to-interp-terms-consdecls =
    is-list;
    filter(is-generatable-consdecl; require(ds-to-interp-terms-consdecl, debug-consdecl-name|"Term generation failed for constructor"))

rules

  ds-to-interp-terms-consdecl:
    dec@ConsDecl(name, kid*, s, _) ->
      compilation-unit |[
        package ~x:<get-opt> TermPkg();
        
        import org.spoofax.interpreter.core.Tools;
        import org.spoofax.interpreter.terms.*;
        import ~x:<get-opt> NativePkg().*;
        import ~x:<get-opt> TopPkg().TypesGen;
        import org.metaborg.meta.lang.dynsem.interpreter.nodes.matching.ITermInstanceChecker;
        import com.oracle.truffle.api.CompilerAsserts;
        import com.oracle.truffle.api.CompilerDirectives.TruffleBoundary;
        import org.apache.commons.lang3.builder.HashCodeBuilder;
        import com.oracle.truffle.api.CompilerDirectives.TruffleBoundary;
        
        public final class x_classname extends ~x:<jclass-term> s {

          public final static String CONSTRUCTOR = ~e:Lit(String([Chars(name)]));
          public final static int ARITY = ~i:<length; int-to-string> kid*;
          
          ~finit*

          ~fdec0*

          ~fget0*
                    
          ~mcreate0*

          private final IStrategoTerm strategoTerm;
          
          @Override
          public int size() {
            return ARITY;
          }
          
          @Override
          public boolean equals(Object obj) {
            if (this == obj)
              return true;
            if (obj == null)
              return false;
            if (getClass() != obj.getClass())
              return false;
            x_classname other = (x_classname) obj;
            bstm2*
            return true;
          }
          
          @Override
          public boolean hasStrategoTerm() {
              return strategoTerm != null;
          }
          
          @Override
          public IStrategoTerm getStrategoTerm() {
              return strategoTerm;
          }
          
          @TruffleBoundary
          @Override
          public String toString() {
            final StringBuilder sb = new StringBuilder();
            sb.append(CONSTRUCTOR);
            sb.append("(");
            bstm3*
            sb.append(")");   
            return sb.toString();
          }
          
          @Override
          @TruffleBoundary
          public int hashCode() {
            return e_hash.toHashCode();
          }
          
          @Override
          public ITermInstanceChecker getCheck() {
            return new ITermInstanceChecker() {
        
              @Override
              public boolean isInstance(Object obj) {
                return TypesGen.~x:$[is[x_classname]](obj);
              }
            };
          }
        }
      ]|
    where
      x_classname := <jclass-term> dec;
      param0* := <ds-to-interp-sorts-to-params> kid*;
      bstm0* := <ds-to-interp-sorts-to-finits> kid*;
      fdec0* := <ds-to-interp-sorts-to-fdecs> kid*;
      fget0* := <ds-to-interp-sorts-to-fgets> kid*;
      e0* := <range; map(\ i -> e |[ ~x:<int-to-string; mkidx> ]| \)> (1, <length; inc> kid*);
      ConstructorType(_, c-srt) := <lookup-def(|Constructors()); lookup-prop(|Type())> (name, <length> kid*);
      if ValueSort() := <lookup-def(|Types());lookup-prop(|SortProp())> c-srt
      then
        mcreate0* := [ class-body-dec |[
          @TruffleBoundary
          public static x_classname create(IStrategoTerm term) {
            return null;
          }
        ]|]
      else
        e1* := <map-with-index((\ i -> e |[ term.getSubterm(~i:<dec; int-to-string>) ]| \, id); ds-to-interp-sorts-to-create-arg)> kid*;
        mcreate0* := [ class-body-dec |[
          @TruffleBoundary
          public static x_classname create(IStrategoTerm term) {
            CompilerAsserts.neverPartOfCompilation();
            assert term != null;
            assert Tools.isTermAppl(term);
            assert Tools.hasConstructor((IStrategoAppl) term, CONSTRUCTOR, ARITY);
            return new x_classname(e1*, term);
          } ]| ]
      end;
      bstm2* := <map-with-index(ds2java-field-eq-check)> kid*;
      e_hash := <foldr(!e |[ new HashCodeBuilder() ]|, ds-to-interp-hashcode-append)> <range> (1, <length; inc> kid*);
      bstm3* := <map-with-index(Fst; !bstm |[ sb.append(~x:<int-to-string; mkidx>); ]|); separate-by(|bstm |[ sb.append(", "); ]|)> kid*
    where
      if [] := kid*
      then
        finit* := class-body-dec* |[
          public final static x_classname SINGLETON = new x_classname();
          private x_classname() {
            this(null);
          }
          
          private x_classname(IStrategoTerm strategoTerm){
            this.strategoTerm = strategoTerm;
          }
        ]|
      else
        finit* := class-body-dec* |[
          public x_classname(param0*) {
            this(e0*, null);
          }
          
          private x_classname(param0*, IStrategoTerm strategoTerm) {
            bstm0*
            this.strategoTerm = strategoTerm;
          }
        ]|
      end
