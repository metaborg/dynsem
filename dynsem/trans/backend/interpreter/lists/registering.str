module backend/interpreter/lists/registering

imports
  signatures/-
  signatures/dynsem/-
  backend/interpreter/-
  backend/interpreter/lists/util

rules

  ds-to-interp-lists-registry-inits =
    mapconcat(require(ds-to-interp-lists-registry-init, debug-listsort-name|"List registry init generation failed for"))
  
  ds-to-interp-lists-registry-init:
    ls@ListSort(es) ->
      bstm* |[
        builderFactories.put(x_listclass.class, new ITermBuildFactory() {
          @Override
          public TermBuild apply(SourceSection source, Object... objects) {
            return x_listbuildgenclass.create(source, (TermBuild[]) objects[0], (TermBuild) objects[1]);
          }
        });
        matcherFactories.put(x_listclass.class, new ITermMatchPatternFactory() {
          @Override
          public MatchPattern apply(SourceSection source, Object... objects) {
            return x_listmatchclass.createUninitialized(source, (MatchPattern[]) objects[0],(MatchPattern) objects[1]);
          }
        });
      ]|
    where
      x_listclass := <jclass-term> ls;
      x_listbuildgenclass := <jclass-termbuilder-ng> ls;
      x_listmatchclass := <jclass-matcher> ls
  
  ds-to-interp-termbuild-access(|x_children):
    (i, ListSort(s)) -> e |[ children[~i:<dec; int-to-string> i] ]|
