module ds2ds/sugar

imports
	signatures/-
	ds
	pp
	
signature
  constructors
    MapExtend : Map * Map -> Map
    MapEntry : Map -> Bind

rules

  format-derivationstyle-editor:
    (selected, position, ast, path, project-path) -> (filename, result)
    with
      Module(name, section*) := <alltd(format-derivationstyle-rule)> ast;
      result := <pp-debug> Module($[[name].derivation], section*);
      filename := <guarantee-extension(|"derivation.ds")> path
  
  format-codestyle-editor:
    (selected, position, ast, path, project-path) -> (filename, result)
    with
      Module(name, section*) := <innermost(format-codestyle-rule)> ast;
      result := <pp-debug> Module($[[name].code], section*);
      filename := <guarantee-extension(|"code.ds")> path

rules // desugaring  
	
	desugar-meta-functions-top = alltd(desugar-functions)
	
	desugar-functions :
		Signatures(sigs1) -> 
		Signatures([Sorts(ss), Constructors(cs) | sigs2])
		where
		  <collect(extract-fun-sorts-constructors); unzip> sigs1 => (ss@[_|_], cs@[_|_]);
		  <alltd(desugar-fundecl)> sigs1 => sigs2
  
  extract-fun-sorts-constructors:
    MetaFunDecl(_, x, lhs-s*, _, _, _) -> (SortDecl(t_name), ConsDecl(x, lhs-s*, t_source, Annos([MetafunAnno()])))
    where
      t_name := $[[x]_[<length; int-to-string> lhs-s*]_Meta];
      t_source := SimpleSort(t_name)
  
  desugar-fundecl:
    MetaFunDecl(ro*, x, lhs-s*, rw1*, rhs-s, rw2*) -> DefaultArrowDecl(ro*, t_source, rw1*, rhs-s, rw2*)
    where
      t_name := $[[x]_[<length; int-to-string> lhs-s*]_Meta];
      t_source := SimpleSort(t_name)

  desugar-top = 
  	innermost(desugar + format-derivationstyle-rule); 
  	alltd(rules-concat)
  
  desugar :
    NativeNoArgFunctionDecl(name, type) -> NativeFunctionDecl(name, [], type)
  
  desugar :
    NullaryConsDecl(c, s, annos) -> ConsDecl(c, [], s, annos)
  
  desugar :
    NullaryExDecl(name) -> ExDecl(name, [])
  
  desugar :
    NoAnnos() -> Annos([])
  
  desugar :
    NullaryNativeConsDecl(c, s) -> NativeConsDecl(c, [], s)

  desugar :
    NullaryNativeOpDecl(c, s) -> NativeOpDecl(c, [], s)
  
  desugar :
    DefaultArrowDecl(ros, l-ty, rwsin, r-ty, rwsout) -> ArrowDecl(ros, l-ty, rwsin, "", r-ty, rwsout)
  
  desugar :
    Dynamic() -> NamedDynamic("")
  
  desugar :
    DynamicEmitted(e) -> NamedDynamicEmitted(e, "")
  
  desugar :
    NamedDynamic(n) -> NamedDynamicEmitted([], n)
  
  desugar :
    List_(x*) -> ListTail(x*, List_([]))
    where <not(?[])> x*

  desugar :
    ListTail([x | xs*], xxs) -> ListTail([x], ListTail(xs*, xxs))
    where
      <not(?[])> xs*

  desugar :
    NoReads() -> Reads([])

  desugar :
    NoArrowROs() -> ArrowROs([]) 

  desugar :
    NoArrowRWs() -> ArrowRWs([])

  desugar :
    Source(lhs) -> Source(lhs, [])

  desugar :
    Target(rhs) -> Target(rhs, [])

  desugar :
    Axiom(form) -> Rule([], "---------", form)
    
  desugar :
    Map_([b | b*@[_|_]]) -> MapExtend(Map_([b]), Map_(b*))
    
  desugar :
    Map_([MapEntry(x)]) -> x

  desugar :
    Strict(con,params1,params2) -> list
     where Cons(_,Cons(_,_)) := params1;
           list := <map( \ param -> Strict(con,[param],params2) \)> params1

  desugar :
    Seqstrict(con,params1,params2) -> list
     where Cons(_,Cons(_,_)) := params1;
           list := <map( \ param -> Seqstrict(con,[param],params2) \)> params1
     // this converts a Rule into a list of Rules
     // so they must later be concatenated, post-desugaring:
  
  desugar :
    RuleWDot(form, prem*) -> RuleW(form, prem*)
  
  desugar :
    RuleDot(prem*, inf, form) -> Rule(prem*, inf, form)
  
  rules-concat :
  	 Rules(list) -> Rules(list1)
  	 where list1 := <map(?Cons(_,_) <+ ( \ n -> Cons(n,Nil()) \)); concat> list

rules

  format-derivationstyle-rule:
    RuleW(form, prem*) -> Rule(prem*, "----------------", form)

  format-codestyle-rule:
    Rule(prem*, _, form) -> RuleW(form, prem*)

rules // sugaring
  
  sugar-all = innermost(sugar + format-codestyle-rule)
  
  sugar :
    NativeFunctionDecl(name, [], type) -> NativeNoArgFunctionDecl(name, type)
  
  sugar :
    ConsDecl(c, [], s, annos) -> NullaryConsDecl(c, s, annos)
  
  sugar :
    Annos([]) -> NoAnnos()
  
  sugar :
    NativeConsDecl(c, [], s) -> NullaryNativeConsDecl(c, s)

  sugar :
    NativeOpDecl(c, [], s) -> NullaryNativeOpDecl(c, s)
  
  sugar :
  	Rule([],infer,conc) -> Axiom(conc)

  sugar :
  	Reads([]) -> NoReads()

  sugar :
    ArrowROs([]) -> NoArrowROs() 

  sugar :
    ArrowRWs([]) -> NoArrowRWs()

  sugar :
  	NamedDynamicEmitted([], name) -> NamedDynamic(name)

  sugar :
  	Target(term,[]) -> Target(term)

  sugar :
  	Source(term,[]) -> Source(term)

