module ds2ds/explication

imports
  signatures/-
  analysis/-
  lib-ds
  ds2ds/-
  
rules

  explication-explicate-editor:
    (_, _, ast, path, project-path) -> (filename, result)
    with
      let
        explicate = 
          in-project-path(
            include-imports
            ; desugar-top
            ; desugar-meta-functions-top
            ; fuse-sections
            ; desugar-varschemes-module
            ; factorize-module
            ; expand-implicits-module
            ; factorize-module
            ; explication-explicate-module
            ; defactorize-module
            ; prettify-vars-module
            ; resugar-varschemes-module
          |project-path)
      in
        Module(name, section*) := <explicate; unmark-vars; sugar-all> ast;
        result := <pp-debug> Module($[[name].explicated], section*);
        filename := <guarantee-extension(|"explicated.ds")> path
      end

  

  explication-explicate-module =
    ?Module(_, _);
    m-in-analysis(
      explication-api-explicate
      ; unrename-all
      ); unmark-vars

signature
  sorts
    Import Component ArrowDef CompDef CompKind
  
  constructors
    Impo : ArrowDef * List(Component) -> Import  
    Comp : CompDef * CompKind -> Component
    RO : CompKind
    RW : CompKind

strategies
  
  explication-api-init-storage(sto-comps, sto-imps) =
    comps-table := <new-hashtable; sto-comps>;
    imps-table := <new-hashtable; sto-imps>;
    arrow-def* := <lookup-def-all(|Arrows())>;
    <map(\ arrow-def -> <hashtable-put(|arrow-def, [])> comps-table \)> arrow-def*;
    <map(\ arrow-def -> <hashtable-put(|arrow-def, [])> imps-table \)> arrow-def*
  
  explication-api-destroy-storage(|comps-table, imps-table) =
    <hashtable-destroy> comps-table;
    <hashtable-destroy> imps-table
  
  explication-api-get-components(|comps, imps):
    arrow-def -> <hashtable-get(|arrow-def)> comps
  
  explication-api-has-component(|comps, imps):
    (arrow-def, comp-def) -> comp'
    where
      <explication-api-get-components(|comps, imps)> arrow-def;
      fetch-elem(?Comp(comp-def, _); ?comp')
  
  explication-api-add-components(|comps, imps):
    (arrow-def, new-compo*) -> compo*
    where
      old-compo* := <hashtable-get(|arrow-def)> comps;
      compo* := <explication-components-nub; explication-util-qsort-comps> [new-compo*, old-compo*];
      <hashtable-put(|arrow-def, compo*)> comps
  
  explication-api-get-imports(|comps, imps):
    arrow-def -> <hashtable-get(|arrow-def)> imps
  
  explication-api-add-imports(|comps, imps):
    (arrow-def, new-imp*) -> imp*
    where
      old-imp* := <hashtable-get(|arrow-def)> imps;
      imp* := <nub> [new-imp*, old-imp*];
      <hashtable-put(|arrow-def, imp*)> imps
  
  
  explication-components-nub:
    comps -> comps'
    with
      tbl := <new-hashtable>;
      comps' := <explication-components-nub(|tbl)> comps;
      <hashtable-destroy> tbl
  
  explication-components-nub(|buff):
    [] -> <map(\ compdef -> Comp(compdef, <hashtable-get(|compdef)> buff) \)> compdefs
    with
      compdefs := <hashtable-keys> buff
      
  
  explication-components-nub(|buff):
    [Comp(compdef, kind') | comps] -> <explication-components-nub(|buff)> comps
    where
      if kind := <hashtable-get(|compdef)> buff
      then
        <hashtable-put(|compdef, <explication-compkind-union> (kind, kind'))> buff
      else
        <hashtable-put(|compdef, kind')> buff
      end
  
  explication-compkind-union =
    if ?(RW(), _) <+ ?(_, RW())
    then
      !RW()
    else
      !RO()
    end
 
  
rules

  explication-api-explicate(s) = 
    debug(!"x1 ");
    where(explication-api-init-storage(?comps-table, ?imps-table));
    debug(!"x2 ");
    where(explication-gather-data-arrowdecl(|comps-table, imps-table));
    debug(!"x3 ");
    where(alltd(explication-gather-data-rule(|comps-table, imps-table)));
    debug(!"x4 ");
    where(explication-compute-explicated-arrows(|comps-table, imps-table));
    debug(!"x5 ");
    s(|comps-table, imps-table);
    debug(!"x6 ");
    where(explication-api-destroy-storage(|comps-table, imps-table))
    ;debug(!"x7 ")

  explication-api-explicate(pre, post) =
    let
      work(|comps-table, imps-table) =
        pre(|comps-table, imps-table);
        explication-explicate-arrow-decls-module(|comps-table, imps-table);
        explication-explicate-rules-module(|comps-table, imps-table);
        post(|comps-table, imps-table)
    in
      explication-api-explicate(work)
    end  
  
  explication-api-explicate =
    let
      noop(|comps-table, imps-table) = id
    in
      explication-api-explicate(noop, noop)
    end
  
  explication-gather-data-arrowdecl(|comps-table, imps-table) =
      lookup-def-all(|Arrows());
      map(explication-associate-decl-components(|comps-table, imps-table));
      try(explication-gather-data-arrowdecl-extend-api(|comps-table, imps-table))

  explication-gather-data-arrowdecl-extend-api(|comps-table, imps-table) = fail

  explication-associate-decl-components(|comps-table, imps-table) =
    ?arrow-def;
    ro* := <lookup-prop(|ROs()); map(\ Label(x) -> Comp(<lookup-def(|Components())> x, RO()) \)> arrow-def;
    rw* := <lookup-prop(|RWs()); map(\ Label(x) -> Comp(<lookup-def(|Components())> x, RW()) \)> arrow-def;
    <explication-api-add-components(|comps-table, imps-table)> (arrow-def, [ro*, rw*])

  explication-gather-data-rule(|comps-table, imps-table):
    r@Rule(prem*, infer, Relation(reads, src@Source(lhs, _), arr, _)) -> r
    where // figure out which arrow definition we are using
      arrow-def := <resolve-applicable-arrow-defsite> (<get-arrow-name> arr, <rulelhs-get-type> lhs)
    where // associate components used in conclusion
      ro* := <explication-use-to-comp> reads;
      rw* := <explication-use-to-comp> src;
      <explication-api-add-components(|comps-table, imps-table)> (arrow-def, [ro*, rw*])
    where // store premise-derived information
      imp* := <collect(explication-gather-data-premise(|comps-table, imps-table); not(?Impo(arrow-def, _)))> prem*;
      <explication-api-add-imports(|comps-table, imps-table)> (arrow-def, imp*)
  
  explication-gather-data-premise(|comps-table, imps-table):
    Formula(Relation(reads, src@Source(lhs, _), arr, _)) -> Impo(arrow-def, <nub> [ex-ro*, ex-rw*])
    where // figure out which arrow definition we are using
      arrow-def := <resolve-applicable-arrow-callsite> (<get-arrow-name> arr, <type-of> lhs)
    where // associate components used in premise
      ro* := <explication-use-to-comp> reads;
      rw* := <explication-use-to-comp> src;
      <explication-api-add-components(|comps-table, imps-table)> (arrow-def, [ro*, rw*])
    where // compute imports
      ex-ro* := <explication-use-to-compdef> reads;
      ex-rw* := <explication-use-to-compdef> src
  
  explication-use-to-comp:
    Reads(r*) -> <map(\ LabelComp(Label(x), _) -> Comp(<lookup-def(|Components())> x, RO()) \)> r*
  
  explication-use-to-comp:
    Source(_, w*) -> <map(\ LabelComp(Label(x), _) -> Comp(<lookup-def(|Components())> x, RW()) \)> w*
  
  explication-use-to-compdef:
    Reads(r*) -> <map(\ LabelComp(Label(x), _) -> Comp(<lookup-def(|Components())> x, RO()) \)> r*
  
  explication-use-to-compdef:
    Source(_, w*) -> <map(\ LabelComp(Label(x), _) -> Comp(<lookup-def(|Components())> x, RW())\)> w*
  
  explication-compute-explicated-arrows(|comps-table, imps-table) =
    opt-with(
      arrow-def* := <lookup-def-all(|Arrows())>;
      // FIXME: this causes an O(N^N) complexity
      stack := <range; map(!arrow-def*); concat> (1, <length> arrow-def*);
      <explication-compute-explicated-arrows-work(|comps-table, imps-table)> stack
    )
  
  explication-compute-explicated-arrows-work(|comps-table, imps-table):
    [] -> []
  
  explication-compute-explicated-arrows-work(|comps-table, imps-table) =
    ?[arrow-def | defs];
    opt-with(
      // imports by arrow-def
      impo* := <explication-api-get-imports(|comps-table, imps-table)> arrow-def;
      // hence imported arrows
      imported-arrow* := <map(?Impo(<id>, _)); nub> impo*;
      // imported components
      imported-compo* := <mapconcat(explication-compute-imported-comps(|impo*, comps-table, imps-table))> imported-arrow*;
      // components declared/gathered so far
      declared-compo* := <explication-api-get-components(|comps-table, imps-table)> arrow-def;
      // thus to be added components
      compo* := <diff(explication-util-compo-eq)> (imported-compo*, declared-compo*);
      // add components
      <explication-api-add-components(|comps-table, imps-table)> (arrow-def, compo*);
      // process rest of stack
      <explication-compute-explicated-arrows-work(|comps-table, imps-table)> defs
    )
  
  explication-compute-imported-comps(|impo*, comps-table, imps-table):
    arrow-def -> new-compo*
    where opt-with(
      // arrow components declared/gathered so far
      compo* := <explication-api-get-components(|comps-table, imps-table)> arrow-def;
      // components excluded from imports
      compo-ex-by-import* := <filter(?Impo(arrow-def, <id>)); !(<id>, compo*); foldl(isect(explication-util-compo-eq))> impo*;
      // components to be added
      new-compo* := <diff(explication-util-compo-eq)> (compo*, compo-ex-by-import*)
    )
  
  explication-explicate-arrow-decls-module(|comps-table, imps-table):
    Module(name, section*) -> Module(name, section'*)
    where
      section'* := <map(try(Signatures(map(try(Arrows(map(explication-explicate-arrow-decl(|comps-table, imps-table))))))))> section*

  explication-explicate-arrow-decl(|comps-table, imps-table):
    ArrowDecl(_, lhs-s, _, arrow-name, rhs-s, _) -> ArrowDecl(ArrowROs(ro*), lhs-s, ArrowRWs(rw*), arrow-name, rhs-s, ArrowRWs(rw*))
    where
      arrow-def := <resolve-applicable-arrow-defsite> (arrow-name, <rw-type> lhs-s);
      (ro-comp*, rw-comp*) := <explication-api-get-components(|comps-table, imps-table); partition(?Comp(_, RO()))> arrow-def;
      (ro*, rw*) := <(map(explication-util-comp-to-label), map(explication-util-comp-to-label))> (ro-comp*, rw-comp*)

  explication-explicate-arrow-decl(|comps-table, imps-table):
    NativeFunDecl(_, cname, kid-srt*, _, out-srt, _) -> NativeFunDecl(ArrowROs(ro*), cname, kid-srt*, ArrowRWs(rw*), out-srt, ArrowRWs(rw*))
    where
      t_sort := SimpleSort($[[cname]_[<length; int-to-string> kid-srt*]_Native]);
      arrow-def := <resolve-applicable-arrow-defsite> ("", <rw-type> t_sort);
      (ro-comp*, rw-comp*) := <explication-api-get-components(|comps-table, imps-table); partition(?Comp(_, RO()))> arrow-def;
      (ro*, rw*) := <(map(explication-util-comp-to-label), map(explication-util-comp-to-label))> (ro-comp*, rw-comp*)

  explication-explicate-rules-module(|comps-table, imps-table):
    Module(name, section*) -> Module(name, section'*)
    where
      section'* := <alltd(explication-explicate-rule(|comps-table, imps-table))> section*

  explication-explicate-rule(|comps-table, imps-table):
    Rule(prem*, infer, Relation(Reads(ro*), Source(lhs, sc*), arr, Target(rhs, tc*))) ->
      Rule(prem'*, infer, Relation(Reads(ro'*), Source(lhs, sc'*), arr, Target(rhs, tc'*)))
    where opt-with(
      arrow-def := <resolve-applicable-arrow-defsite> (<get-arrow-name> arr, <rulelhs-get-type> lhs)
    )
    where opt-with(
      // init storage
      compgen-table := <new-hashtable>;
      // introduce ROs adding missing ones
      ro'* := <explication-intro-comps(|comps-table, imps-table, compgen-table, arrow-def, RO())> ro*;
      // introduce RWs adding missing ones
      sc'* := <explication-intro-comps(|comps-table, imps-table, compgen-table, arrow-def, RW())> sc*;
      // explicate premises
      prem'* := <alltd(explication-explicate-premise(|comps-table, imps-table, arrow-def, compgen-table))> prem*;
      // read missing RWs
      tc'* := <explication-use-comps(|comps-table, imps-table, compgen-table, arrow-def, RW())> tc*;
      // destroy storage
      <hashtable-destroy> compgen-table
    )
  
  explication-explicate-premise(|comps-table, imps-table, rule-arrow-def, compgen-table):
    Formula(Relation(Reads(ro*), Source(lhs, sc*), arr, Target(rhs, tc*))) ->
      Formula(Relation(Reads(ro'*), Source(lhs, sc'*), arr, Target(rhs, tc'*)))
    where opt-with( // figure out which arrow definition we are using
      arrow-def := <resolve-applicable-arrow-callsite> (<get-arrow-name> arr, <type-of> lhs)
    )
    where opt-with(
      // read missing ROs
      ro'* := <explication-use-comps(|comps-table, imps-table, compgen-table, arrow-def, RO())> ro*;
      // read missing RWs
      sc'* := <explication-use-comps(|comps-table, imps-table, compgen-table, arrow-def, RW())> sc*;
      // introduce RWs adding missing ones
      tc'* := <explication-intro-comps(|comps-table, imps-table, compgen-table, arrow-def, RW())> tc*
    )
  
  explication-explicate-premise(|comps-table, imps-table, rule-arrow-def, compgen-table):
    CaseMatch(tb, case*) -> CaseMatch(tb, case'*)
    where opt-with(
      // explicate the cases in forked compgen-tables returning [(case', compgen-table-fork)] 
      case-compgen-table* := <map(explication-explicate-premise-fork(|comps-table, imps-table, rule-arrow-def, compgen-table))> case*;
      // introduce fresh variables for all the RW components
      all-rw* := <explication-api-get-components(|comps-table, imps-table); explication-util-filter-transform-compdefs(explication-util-comp-to-compdef|RW())> rule-arrow-def;
      <map(explication-api-compgen-intro(|compgen-table))> all-rw*;
      case'* := <map(explicate-2-introduce-synonyms-in-case(|all-rw*, compgen-table))> case-compgen-table*
    )
  
  explication-explicate-premise-fork(|comps-table, imps-table, rule-arrow-def, compgen-table):
    t -> (<alltd(explication-explicate-premise(|comps-table, imps-table, rule-arrow-def, compgen-table-fork))> t, compgen-table-fork)
    where opt-with(
      compgen-table-fork := <hashtable-copy> compgen-table
    )
  
  explicate-2-introduce-synonyms-in-case(|all-rw*, compgen-table):
    (CasePattern(pat, prem*), compgen-table-fork) -> CasePattern(pat, prem'*)
    where opt-with(
      prem'* := <explicate-2-introduce-synonyms-in-case-prems(|all-rw*, compgen-table)> (prem*, compgen-table-fork)
    )

  explicate-2-introduce-synonyms-in-case(|all-rw*, compgen-table):
    (CaseOtherwise(prem*), compgen-table-fork) -> CaseOtherwise(prem'*)
    where opt-with(
      prem'* := <explicate-2-introduce-synonyms-in-case-prems(|all-rw*, compgen-table)> (prem*, compgen-table-fork)
    )
  
  explicate-2-introduce-synonyms-in-case-prems(|all-rw*, compgen-table):
    (prem*, compgen-table-fork) -> [prem*, synonym*]
    where opt-with(
      synonym* := <filter(explicate-2-introduce-synonym(|compgen-table-fork, compgen-table))> all-rw*
    )
  
  explicate-2-introduce-synonym(|compgen-table-left, compgen-table-right):
    comp -> Formula(Match(Var(v-left), Var(v-right)))
    where opt-with(
      v-left := <hashtable-get(|comp)> compgen-table-left;
      v-right := <hashtable-get(|comp)> compgen-table-right
    )
    where 
      <not(eq)> (v-left, v-right)
  
  explication-use-comps(|comps-table, imps-table, compgen-table, arrow-def, kind):
    labelcomp* -> <explication-util-qsort-comps> [labelcomp*, extra-labelcomp*]
    where opt-with(
      // used components
      comp-use* := <map(\ LabelComp(Label(x), _) -> <lookup-def(|Components())> x \)> labelcomp*;
      // definition components
      comp-def* := <explication-api-get-components(|comps-table, imps-table); explication-util-filter-transform-compdefs(explication-util-comp-to-compdef|kind)> arrow-def;
      // thus missing components
      comp-extra* := <diff> (comp-def*, comp-use*);
      // lookup extra names and convert to labelcomp
      extra-labelcomp* := <map(explication-api-compgen-use(|compgen-table, kind))> comp-extra*
    )
  
  explication-intro-comps(|comps-table, imps-table, compgen-table, arrow-def, kind):
    labelcomp* -> <explication-util-qsort-comps> [labelcomp*, extra-labelcomp*]
    where opt-with(
      // used components
      comp-use* := <map(explication-api-compgen-register(|compgen-table, kind))> labelcomp*;
      // definition components
      comp-def* := <explication-api-get-components(|comps-table, imps-table); explication-util-filter-transform-compdefs(explication-util-comp-to-compdef|kind)> arrow-def;
      // thus missing components
      comp-extra* := <diff> (comp-def*, comp-use*);
      // register and convert to labelcomp
      extra-labelcomp* := <map(explication-api-compgen-intro(|compgen-table))> comp-extra*
    )
  
  explication-util-comp-to-compdef = ?Comp(<id>, _)
  
  explication-util-compo-eq = ?(Comp(x, _), Comp(x, _))
  
  explication-util-compo-gt = ?(Comp((_, x, _), _), Comp((_, y, _), _)); !(x, y); string-gt
  
  explication-util-compo-gt = ?(LabelComp(Label(x), _), LabelComp(Label(y), _)); !(x, y); string-gt
  
  explication-util-qsort-comps = qsort(explication-util-compo-gt)
  
  explication-util-comp-to-label:
    Comp(compdef, _) -> Label(<def-get-name> compdef)
  
  explication-util-filter-transform-compdefs(s|kind) =
    filter(?Comp(_, kind); s)
  
  explication-api-compgen-register(|compgen-table, kind):
    LabelComp(Label(x), Var(v)) -> comp-def
    where opt-with(
      comp-def := <lookup-def(|Components())> x;
      <hashtable-put(|comp-def, v)> compgen-table
    )
  
  explication-api-compgen-intro(|compgen-table):
    comp-def -> label-comp
    where opt-with(
      v := <explication-api-compgen-intro-gen> comp-def;
      label-comp := <explication-api-compgen-intro-record(|compgen-table)> (comp-def, v)
    )

  explication-api-compgen-intro-gen:
    comp-def -> v
    where
      v := <newname> $[_lifted_[<lookup-prop(|Type()); derw-type; get-sort-name> comp-def]]
  
  explication-api-compgen-intro-record(|compgen-table):
    (comp-def, v) -> LabelComp(Label(<def-get-name> comp-def), Var(v))
    where opt-with(
      <hashtable-put(|comp-def, v)> compgen-table
    )
  
  explication-api-compgen-use(|compgen-table, kind):
    comp-def -> LabelComp(Label(<def-get-name> comp-def), Var(<hashtable-get(|comp-def)> compgen-table))
