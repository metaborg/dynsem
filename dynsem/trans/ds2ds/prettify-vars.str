module ds2ds/prettify-vars

imports
  signatures/-
  analysis/-
  ds2ds/-
  ds
  lib-ds
  
strategies
    
  prettify-vars-module =
    ?Module(_, _);
    m-in-analysis(prettify-vars-all; unrename-all)
  
  prettify-vars-all = alltd(prettify-vars-rule)
  
  prettify-vars-rule:
    r@Rule(p*, infer, Relation(reads, src, arrow, tgt)) -> Rule(p'*, infer, Relation(reads', src', arrow, tgt'))
    where {| PrettifyVar, NextIdx:
      rule-vars := <collect-all(?Var(<unrename-var>) + ?Const(<unrename-var>))> r;
      reads' := <alltd(prettify-var-def(|rule-vars))> reads;
      src' := <alltd(prettify-var-def(|rule-vars))> src;
      p'* := <alltd(prettify-var-def(|rule-vars)); alltd(prettify-var-ref)> p*;
      tgt' := <alltd(prettify-var-ref)> tgt 
    |}
  
  prettify-var-def(|rule-vars):
    Var(x) -> Var(<PrettifyVar> x)
  
  prettify-var-def(|rule-vars):
    Const(x) -> Const(<PrettifyVar> x)
  
  prettify-var-def(|rule-vars):
    Var(x) -> Var(<prettify-var-def-helper(|rule-vars)> x)
    where
      <not(PrettifyVar)> x

  prettify-var-def(|rule-vars):
    Const(x) -> Const(<prettify-var-def-helper(|rule-vars)> x)
    where
      <not(PrettifyVar)> x
  
  prettify-var-def-helper(|rule-vars):
    x -> x'
    where
      <string-starts-with(|"_lifted")> x;
      x-def := <lookup-def(|Vars())> x;
      x-ty := <lookup-prop(|Type())> x-def;
      varscheme-def* := <get-reverse-matching-varschemes(get-all-varschemes)> x-ty;
      if scheme-def := <Hd> varscheme-def*
      then
        x-prefix := <def-get-name> scheme-def
      else
        x-prefix :=
          <derw-type; get-sort-name; lower-case; string-as-chars(\ [x|_] -> [x] \)> x-ty
      end;
      x' := <pesimistic-next-variable(|rule-vars)> x-prefix;
      rules(PrettifyVar: x -> x')

  prettify-var-ref = VarRef(PrettifyVar) + ConstRef(PrettifyVar)
  
  pesimistic-next-variable(|vars):
    prefix -> name
    where
      let
        next-idx = <NextIdx> prefix <+ rules(NextIdx: prefix -> 0); !0
        make-name = !$[[prefix][<id>]]
      in
        idx := <next-idx; repeat-until(inc, not(make-name; \ x -> <fetch-elem(?x)> vars \))>;
        rules(NextIdx: prefix -> idx);
        name := <make-name> idx
      end
      
