module ds2ds/explicate-arrow-types

imports
  signatures/-
  analysis/-

signature
  constructors
    NamedDynamicEmitted : List(LabelComp) * IDNOHYPH * Type -> Rel

strategies

  explicate-arrow-types-editor:
    (_, position, ast, path, project-path) -> (filename, result)
    with
      result := <explicate-arrow-types> ast;
      filename := <guarantee-extension(|".aterm")> path

  explicate-arrow-types =
    ?Module(_, _);
    m-in-analysis(
      alltd(explicate-arrow-type-relation);
      unrename-all
    )

  explicate-arrow-types-noanalysis =
    ?Module(_, _);
    alltd(explicate-arrow-type-relation)

rules

  explicate-arrow-type-relation:
    Relation(reads, src@Source(t, _), NamedDynamicEmitted(e, aname), tgt)
      -> Relation(reads, src, NamedDynamicEmitted(e, aname, <derw-type> atype), tgt)
    with
      arrow-def := <get-applicable-arrowdef(flip(type-coerce-direct(fail)))> (<lookup-defs(|Arrows())> aname, <type-of> t);
      ArrowType(atype, _) := <lookup-prop(|Type())> arrow-def

rules // Utility predicate for new NamedDynamicEmitted ATerm

  get-arrow-name:
    NamedDynamicEmitted(_, name, _) -> name
 