module ds2ds/core

imports
  signatures/-
  signatures/dynsem/-
  analysis/resolve-includes
  analysis/lib-analysis
  analysis/mark-references
  ds2ds/-
  backend/common/opts
  pp

rules

  module-to-core-editor:
    (_, position, ast, path, project-path) -> (filename, result)
    with
      Module(name, section*) := <module-to-core(|path, project-path); unmark-vars; sugar-all> ast;
      result := <pp-debug> Module($[[name].core], section*);
      filename := <guarantee-extension(|"core.ds")> path
  
  module-to-core-ast-editor:
    (_, position, ast, path, project-path) -> (filename, result)
    with
      core-mod := <module-to-core(|path, project-path); unmark-vars; sugar-all> ast;
      Module(name, section*) := <innermost(desugar-native-ops)> core-mod;
      result := Module($[[name].core], section*);
      filename := <guarantee-extension(|"core.aterm")> path
  
strategies

  module-to-core(|path, project-path) =
    in-project-path(
      include-imports
      ; desugar-top
      ; desugar-meta-functions-top
      ; fuse-sections
      ; desugar-guarded-binds-module
      ; desugar-varschemes-module
      ; add-extra-typeannos-module
      ; desugar-aliases-module
      ; factorize-module
      ; expand-implicits-module
      ; factorize-module
      ; expand-defaultcomponents-module-top
      ; explication-explicate-module
      ; copy-propagation-module
      ; eliminate-dead-premises-module
      ; insert-wildcards-module
      ; defactorize-module
      ; prettify-vars-module
    |project-path)
