module extra-typeannos

imports
  signatures/-
  ds
  analysis/-

rules

  add-extra-typeannos-module:
    m@Module(_, _) -> Module($[[name]_typeannos], section*)
    where
      <m-in-analysis(add-extra-typeannos; unrename-all); unmark-vars> m => Module(name, section*)
      
  add-extra-typeannos = alltd(add-extra-typeannos-rule); alltd(add-extra-typeannos-term)
  
  add-extra-typeannos-rule:
    Rule(p*, infer, Relation(Reads(r*), Source(lhs, sc*), arr@NamedDynamicEmitted(_, arrow-name), Target(l@List([]), tc*))) ->
      Rule(p*, infer, Relation(Reads(r*), Source(lhs, sc*), arr, Target(Cast(l, bu-ty-str), tc*)))
    where
      arrow-ty* := <lookup-def(|Arrows()); lookup-props(|Type())> arrow-name;
      ArrowType(ma-ty, bu-ty) := <type-of-applicable-arrow(type-coerce-full(id))> (arrow-ty*, <type-of> lhs);
      bu-ty-str := <derw-type> bu-ty
  
  add-extra-typeannos-rule:
    Rule(p*, infer, Relation(Reads(r*), Source(lhs, sc*), arr@NamedDynamicEmitted(_, arrow-name), Target(m@Map([]), tc*))) ->
      Rule(p*, infer, Relation(Reads(r*), Source(lhs, sc*), arr, Target(Cast(m, bu-ty-str), tc*)))
    where
      arrow-ty* := <lookup-def(|Arrows()); lookup-props(|Type())> arrow-name;
      ArrowType(ma-ty, bu-ty) := <type-of-applicable-arrow(type-coerce-full(id))> (arrow-ty*, <type-of> lhs);
      bu-ty-str := <derw-type> bu-ty

  add-extra-typeannos-term:
    Con(name, c*) -> Con(name, c'*)
    where
      c-def := <lookup-def(|Constructors())> (name, <length> c*);
      ConstructorType(c-ty*, ty) := <lookup-prop(|Type())> c-def;
      c'* := <zip(add-extra-typeannos-term <+ Snd)> (c-ty*, c*)
  
  add-extra-typeannos-term:
    (_, c@Con(_, _)) -> <try(add-extra-typeannos-term)> c
  
  add-extra-typeannos-term:
    (lty@ListType(ty), ListTail(h, t)) -> ListTail(h', t')
    where
      h' := <add-extra-typeannos-term <+ Snd> (ty, h);
      t' := <add-extra-typeannos-term <+ Snd> (lty, t)
  
  add-extra-typeannos-term:
    (ty, m@Map([])) -> Cast(m, <derw-type> ty)
  
  add-extra-typeannos-term:
    (ty, l@List([])) -> Cast(l, <derw-type> ty)

