module ds2ds/abruptions/abruptions

imports
  signatures/-
  signatures/dynsem/-
  analysis/-
//  ds2ds/fuse-sections
  ds2ds/explication
  
strategies

  abruptions-enable-module =
    try(
      UsesAbruptions <+
      where(
        ?Module(_, <id>);
        fetch-elem(?Signatures(<id>));
        fetch-elem(?Exceptions(_));
        rules(UsesAbruptions: t -> t)
      )
    )
    
  abruptions-new-scope(s) = {| UsesAbruptions:
    s
  |}
  
  abruptions-are-enabled = where(UsesAbruptions)
  
strategies
  
  abruptions-name-sort-thrown   = !"_Thrown"
  abruptions-name-sort-throwing = !"_Throwing"
  abruptions-name-sort-catching = !"_Catching"
  abruptions-name-sort-val      = !"_ExcVal"
  abruptions-name-sort-unit     = !"U"
  abruptions-name-component     = !"_Exc"
  abruptions-name-arrow-handle  = !"handle"
  abruptions-name-arrow-raise   = !"raise"
  abruptions-name-arrow-handler = !"handler"
  abruptions-name-ctr-ok        = !"OK"
  
  
  abruptions-builtin-signatures-gen =
    srt-decls := Sorts([
                  SortDecl(<abruptions-name-sort-thrown>),
                  SortDecl(<abruptions-name-sort-throwing>),
                  SortDecl(<abruptions-name-sort-catching>),
                  SortDecl(<abruptions-name-sort-val>)
                  ]);
    ctr-decls := Constructors([
                  ConsDecl(<abruptions-name-ctr-ok>, [], SimpleSort(<abruptions-name-sort-thrown>), Annos([]))
                 ]);
    cmp-decls := Components([
                  CompDeclDefault(<abruptions-name-component>, SimpleSort(<abruptions-name-sort-thrown>), Con(<abruptions-name-ctr-ok>, []))
                  ]);
    arr-decls := Arrows([
                  ArrowDecl(ArrowROs([]), SimpleSort(<abruptions-name-sort-throwing>), ArrowRWs([]), "", SimpleSort(<abruptions-name-sort-val>), ArrowRWs([])), 
                  NativeFunDecl(ArrowROs([]), <abruptions-name-arrow-handle>, [SimpleSort(<abruptions-name-sort-throwing>), SimpleSort(<abruptions-name-sort-catching>)], ArrowRWs([]), SimpleSort(<abruptions-name-sort-val>), ArrowRWs([])), 
                  NativeFunDecl(ArrowROs([]), <abruptions-name-arrow-handle>, [SimpleSort(<abruptions-name-sort-throwing>), SimpleSort(<abruptions-name-sort-catching>), SimpleSort(<abruptions-name-sort-throwing>)], ArrowRWs([]), SimpleSort(<abruptions-name-sort-val>), ArrowRWs([])), 
                  NativeFunDecl(ArrowROs([]), <abruptions-name-arrow-raise>, [SimpleSort(<abruptions-name-sort-thrown>)], ArrowRWs([]), SimpleSort(<abruptions-name-sort-unit>), ArrowRWs([])),
                  MetaFunDecl(ArrowROs([]), <abruptions-name-arrow-handler>, [SimpleSort(<abruptions-name-sort-thrown>), SimpleSort(<abruptions-name-sort-catching>)], ArrowRWs([]), SimpleSort(<abruptions-name-sort-val>), ArrowRWs([]))
                  ]);
    !Signatures([srt-decls, ctr-decls, cmp-decls, arr-decls])
  
  abruptions-store-built-ins =
    abruptions-enable-module; abruptions-are-enabled
    < abruptions-builtin-signatures-gen; store-signatures
    + id 
  
  abruptions-store = Exceptions(map(abruptions-store-exception))
  
  abruptions-store-exception:
    e@ExDecl(name, kid*) -> e
    with
      arity := <length> kid*;
      c-def := <store-def(|Constructors())> (name, arity);
      <store-prop(|Type(), c-def)> ConstructorType(<rw-type> kid*, <abruptions-name-sort-thrown>);
      <store-prop(|ConsKind(), c-def)> LanguageCons()
 
  abruptions-inject-definitions-top =
    abruptions-are-enabled
      < abruptions-inject-definitions
      + id
  
  abruptions-inject-definitions:
    Module(name, section*) -> Module(name, section'*)
    where
      section'* := <map(try(abruptions-inject-definitions))> section*

  abruptions-inject-definitions =
    Signatures(partition(abruptions-inject-definitions); (Hd, id); conc)
  
  abruptions-inject-definitions:
    e@Exceptions(exc) -> sig-part*
    with
      Signatures(sig-part*) := <abruptions-builtin-signatures-gen>
  
  explication-gather-data-arrowdecl-extend-api(|comps-table, imps-table) =
    abruptions-are-enabled < abruptions-explicate-arrow-imports(|comps-table, imps-table) + fail
  
  abruptions-explicate-arrow-imports(|comps-table, imps-table) =
    // store component imports for handler/2
    // nothing to do here because handler/2 is actually builtin but not native
    fail
    
  abruptions-explicate-arrow-imports(|comps-table, imps-table) =
    // store component imports for handle/2 and handle/3
    // add dependency of handle/2 and handle/3 on the components of _Throwing --> ... and handler/2 --> ...
    throwing-ty := <abruptions-name-sort-throwing>;
    throwing-arrow-def := <resolve-applicable-arrow-defsite> ("", throwing-ty);
    handler-arrow-def := <resolve-applicable-arrow-defsite> ("", <rw-type> SimpleSort($[[<abruptions-name-arrow-handler>]_2_Meta]));
    imp* := [Impo(throwing-arrow-def, []), Impo(handler-arrow-def, [])];
    arrow-handle-2-def := <resolve-applicable-arrow-defsite> ("", <rw-type> SimpleSort($[[<abruptions-name-arrow-handle>]_2_Native]));
    arrow-handle-3-def := <resolve-applicable-arrow-defsite> ("", <rw-type> SimpleSort($[[<abruptions-name-arrow-handle>]_3_Native]));
    <explication-api-add-imports(|comps-table, imps-table)> (arrow-handle-2-def, imp*);
    <explication-api-add-imports(|comps-table, imps-table)> (arrow-handle-3-def, imp*);
    fail
  
  abruptions-explicate-arrow-imports(|comps-table, imps-table) =
    // the raise arrow will consume all components
    // that were passed into handle/2 and handle/3
    // ==> because in the native implementation it needs to capture them in the exception it is throwing
    arrow-handle-2-def := <resolve-applicable-arrow-defsite> ("", <rw-type> SimpleSort($[[<abruptions-name-arrow-handle>]_2_Native]));
    arrow-handle-3-def := <resolve-applicable-arrow-defsite> ("", <rw-type> SimpleSort($[[<abruptions-name-arrow-handle>]_3_Native]));
    arrow-raise-def := <resolve-applicable-arrow-defsite> ("", <rw-type> SimpleSort($[[<abruptions-name-arrow-raise>]_1_Native]));
    <explication-api-add-imports(|comps-table, imps-table)> (arrow-raise-def, [Impo(arrow-handle-2-def, []), Impo(arrow-handle-3-def, [])]);
    fail

    
