module analysis/query

imports
  signatures/ds-sig
  ds

imports
  analysis/constructors
  analysis/lib-analysis
  analysis/analysis-rules

rules
  
  type-of:
    Wld() -> ALPHATYPE()

  type-of:
    As(v, _) -> <type-of> v
  
  type-of:
    Cast(_, ty) -> <rw-type> ty
  
  type-of:
    Var(v) -> <type-of> VarRef(v)

  type-of:
    ListTail(elems, tl) -> l-ty
    where
      l-ty := ListType(<fetch-elem(not(?Wld()); type-of)> elems)
      <+
      l-ty := <type-of> tl
  
  type-of:
    List(elems) -> ListType(<fetch-elem(not(?Wld()); type-of)> elems)
  
  type-of:
    Con(name, c*) -> c-ty
    where
      ConstructorType(_, c-ty) := <lookup-def(|Constructors()); lookup-prop(|Type())> (name, <length> c*) 

  type-of:
    Tuple(t*) -> TupleType(<map(type-of)> t*)

  type-of =
    type-check-build-helper

  get-applicable-arrowdef(coerce-s):
    (arrow-def*, term-ty) -> <fetch-elem(where(lookup-prop(|Type()); \ ArrowType(ex-ty, _) -> <coerce-s> (ex-ty, term-ty) \))> arrow-def*

//  lookup-applicable-arrow-def:
//    term-ty -> <get-applicable-arrowdef(type-coerce-direct(fail))> (<lookup-def-all(|Arrows())>, term-ty)

//  type-of-applicable-arrow(coerce-s):
//    (arrow-type*, term-ty) -> <fetch-elem(where(\ ArrowType(ex-ty, _) -> <coerce-s> (ex-ty, term-ty) \))> arrow-type*

//  lookup-applicable-arrow-def:
//    type -> <lookup-def-all(|Arrows()); filter(\ a-def -> (a-def, <lookup-props(|Type()); fetch-elem(where(is-applicable-arrow-def(|type)))> a-def) \); make-set>
//
//  is-applicable-arrow-def(|type-name) =
//    ?ArrowType(type-name, _)
//    <+
//    ?ArrowType(ltype-name, _);
//    <type-coerce-direct(fail)> (type-name, ltype-name)
//
//  lookup-arrow-def:
//    (c-ty, arrow-name) -> <fetch-elem(where(Fst; def-get-name; ?arrow-name))> arrow*
//    where
//      <lookup-applicable-arrow-def> c-ty => arrow*

rules // projections
  
  get-arrow-source-type:
    ArrowType(src-ty, _) -> src-ty
  
  get-arrow-target-type:
    ArrowType(_, tgt-ty) -> tgt-ty

rules // type properties
  
  is-systemsort:
    sort -> sort
    where
      SystemSort() := <lookup-def(|Types()); lookup-prop(|SortKind())> sort

rules /* variable scheme queries */

  get-var-name:
    Var(x) -> x
  
  get-var-name:
    VarRef(x) -> x

  get-label-name:
    Label(x) -> x

  get-all-varschemes =
    <concat> [<get-all-component-defs>, <get-all-varscheme-defs>]    

  get-all-component-defs = lookup-def-all(|Components())
  
  get-all-varscheme-defs = lookup-def-all(|VarSchemes())

  get-reverse-matching-varschemes(get-definitions) =
    ?type;
    get-definitions; filter(is-type-matches-varscheme(|type))

  is-type-matches-varscheme(|type) =
    where(
      lookup-prop(|Type());
      ?type    
    )

  get-unique-matching-varscheme(get-definitions) =
    get-matching-varschemes(get-definitions);
    Hd

  get-matching-varschemes(get-definitions) =
    not(is-string);
    (get-var-name <+ get-label-name);
    get-matching-varschemes(get-definitions)

  get-matching-varschemes(get-definitions):
    used-word -> <get-definitions; filter(is-name-matches-varscheme(|used-word))>
    where is-string
  
  is-name-matches-varscheme(|used-word):
    scheme-def -> scheme-def
    where
      scheme-name := <def-get-name> scheme-def
    where
      used-word-chars := <explode-string> used-word;
      scheme-word-chars := <explode-string> scheme-name;
      used-word-suffix-chars := <left-match> (used-word-chars, scheme-word-chars);
      if not(!used-word-suffix-chars => [])
      then
	      if not(<Hd> used-word-suffix-chars => '_')
	      then
	        <filter(not(?'\''); not(MkSingleton; dec-chars-to-int))> used-word-suffix-chars => []
	      end
	    end












