module generation/explicate_nabl2_annotations

imports
  signatures/ds-sig
  signatures/Signatures-sig
  lib-ds

signature
	
	sorts Nabl2Sorts constructors
		TypeSig : Name * List(Nabl2Sorts) -> Nabl2Sorts
		TypeSort : Nabl2Sorts
		CGenSig : Nabl2Sorts * Nabl2Sorts * List(Nabl2Sorts) * Nabl2Sorts -> Nabl2Sorts
		SortNoArgs : String -> Nabl2Sorts
		Sort : String * List(Nabl2Sorts) -> Nabl2Sorts
		Type : Nabl2Sorts
		NoType : Nabl2Sorts
		Name : String -> Nabl2Sorts

rules

	// Signature of cgen_sig_map:
	//
	//   GeneratorName -> (SortName * List(Kind) * Option(Kind))
	// where
	//   GeneratorName = String 
	//   Kind = Scope | Type

	explicate-nabl2-annotations(|nabl2_sig):
		mod -> mod''''
		with
			(type_sigs, cgen_sigs, subterm_sigs) := nabl2_sig;
			type_constructors := <map(type-sig-to-consdecl)> type_sigs;
			mod' := <foldr(!mod, add-cgen-sigs)> cgen_sigs;
			cgen_sig_map := <new-hashtable>;
			<map(\ CGenSig(Name(cgen_name), sort, _, _) ->
						 <hashtable-put(|cgen_name, SimpleSort(<normalize-sort; postify> sort))> cgen_sig_map \)> cgen_sigs;
			// Iterate through subterm signatures and update ConsDecls in Module accordingly
			mod'' := <foldr(!mod', transform-subterm-sig(|cgen_sig_map))> subterm_sigs;
			// Calculate all sorts that occur in an NaBL2 signature
			cgen_sorts := <get-all-cgen-sorts> cgen_sigs;
			// Update all derived sorts (all sorts that do not occur in an NaBL2 signature)
			mod''' := <postify-derived-sorts-in-consdecls(|cgen_sorts)> mod'';
			// Update arrow signatures and rules accordingly
			mod'''' := mod'''

rules // Translating NaBL2 signatures to DynSem

	type-sig-to-consdecl:
		TypeSig(name, params) -> ConsDecl(name, <map(type-param-to-consdecl-param)> params, SimpleSort("Type"), Annos([]))
		
	type-param-to-consdecl-param:
		TypeSort() -> SimpleSort("Type")

rules // Augmenting module with sort declarations and constructors for explicated term sorts

	// Generates a new sort declaration and constructor for each annotated term
	add-cgen-sigs:
		(CGenSig(Name(cgen_name), sort, ann_list, type), mod) ->
			mod'
		with
			sort' := <normalize-sort> sort;
			( // Check that sort is actually defined:
				<check-sort-defined> (mod, sort');
				scope_kind_list := <map(kindify)> ann_list;
				type_kind_list := <kindify-type> type;
				cons_decl :=
					ConsDecl(<postify-constructor> sort', <concat> [scope_kind_list, type_kind_list, [sort']], SimpleSort(<postify> sort'), Annos([]));
				mod' :=
					<alltd(\ Sorts(ss) -> Sorts(<add-after(|SortDecl(<get-sort-name> sort'))> (SortDecl(<postify> sort'), ss)) \);
					 alltd(\ Constructors(cs) -> Constructors([cons_decl|cs]) \)> mod
			) <+
			(
				<concat-strings; debug> ["ERROR: Sort ", <normalize-sort; get-sort-name> sort, " is used in NaBL2 signature, but is ill-defined!"];
				mod' := mod
			)

	check-sort-defined:
		(mod, SimpleSort(x)) -> 1
		where
			<oncetd(?SortDecl(x))> mod

	check-sort-defined:
		(mod, ListSort(x)) -> 1
		where
			<check-sort-defined> (mod, x)
			
	check-sort-defined:
		(mod, MapSort(x, y)) -> 1
		where
			<check-sort-defined> (mod, x);
			<check-sort-defined> (mod, y)
			

rules // Replace occurrences of non-explicated term sorts with explicated ones

	// FIXME: should be applied to arrow declarations too
	transform-subterm-sig(|cgen_sig_map):
		(((constructor, arity), subterm_sig), mod) ->
			mod'
		with
			mod' :=
				<bottomup(try({
						params, s, a, params:
							?ConsDecl(constructor, params, s, a);
							arity := <length> params;
							!ConsDecl(constructor, <replace-ns(|cgen_sig_map, subterm_sig)> params, s, a)
					}))> mod

rules // Replacing sub-term positions with explicated term sorts

	replace-ns(|cgen_sig_map, subterm_sig):
		params -> params
		where
			subterm_sig := []
			
	replace-ns(|cgen_sig_map, subterm_sig):
		params -> params''
		where
			[(cgen, subterm_pos)|ss'] := subterm_sig;
			sort := <hashtable-get(|cgen)> cgen_sig_map;
			params' := <replace-n> (subterm_pos, sort, params);
			params'' := <replace-ns(|cgen_sig_map, ss')> params'
			
	replace-n:
		(0, x, [_|zs]) -> [x|zs]
		
	replace-n:
		(n, x, [y|zs]) -> [y|<replace-n> (<subt> (n, 1), x, zs)]
		where
			<gt> (n, 0)

rules // Map NaBL2 sorts onto DynSem sorts

	normalize-sort:
		SortNoArgs(sort_name) -> SimpleSort(sort_name)
		
	normalize-sort:
		Sort("List", [sort]) -> ListSort(<normalize-sort> sort)

rules // Postify: generate names for annotated sorts and their constructors

	postify:
		SimpleSort(sort_name) -> <conc-strings> ("Post", sort_name)
		
	postify-constructor:
		SimpleSort(sort_name) -> <conc-strings> (first_char, "_")
		with
			first_char := 
        <string-as-chars(\ [x|_] -> <upper-case-chars> [x] \)> sort_name

	postify:
		ListSort(sort) -> <conc-strings> ("PostL", <get-sort-name> sort)
		
	postify-constructor:
		ListSort(sort) -> <concat-strings> ["L", <get-sort-name> sort, "_"]

// FIXME: Maps?

rules // Kindify: translate NaBL2 kinds to DynSem sorts

	// Note that these names follow the scopegraph-api;
	// they can/will be renamed by DS2V sort aliases when mapping to Coq
	kindify:
		"s" -> SimpleSort("Scope")
	
	kindify:
		Type() -> SimpleSort("Type")

	kindify-type:
		Type() -> [SimpleSort("Type")]
	
	kindify-type:
		None() -> []

	kindify-type:
		NoType() -> []

rules // Postify derived sorts in ConsDecl's
	
	postify-derived-sorts-in-consdecls(|cgen_sorts):
		mod ->
			<alltd({cons, cons': \
				cons@ConsDecl(cname, cparams, sort, annos) ->
					cons'
				where
					(( // If sort is a derived sort (i.e., not a cgen sort)
						not(<elem> (sort, cgen_sorts));
						cons' :=
							ConsDecl(cname,
							<map(
								alltd({ sort' :
									?sort';
									<elem> (sort', cgen_sorts);
									!SimpleSort(<postify> sort')
								}))> cparams, sort, annos)
					) <+
					(cons' := cons)) \})> mod

rules // Postify 

rules // Calculating constraint generation sorts (i.e., which sorts have a constraint generation function associated with them)

	get-all-cgen-sorts:
		cgen_sigs -> <map(get-all-cgen-sort-cgensig); concat> cgen_sigs
		
	get-all-cgen-sort-cgensig:
		CGenSig(_, sort, _, _) -> [<normalize-sort> sort, SimpleSort(<normalize-sort; postify> sort)]

	get-cgen-sorts:
		cgen_sigs -> <map(get-cgen-sort-cgensig)> cgen_sigs
		
	get-cgen-sort-cgensig:
		CGenSig(_, sort, _, _) -> <normalize-sort> sort

rules // Utility for adding an element to a list before/after a particular element, or at the end of the list, if the element is not found
	
	add-after(|e):
		(x, []) -> [x]

	add-after(|e):
		(x, [e|xs]) -> [e, x|xs]
		
	add-after(|e):
		(x, [y|xs]) -> [y|zs]
		where
			!(x, y); not(equal);
			zs := <add-after(|e)> (x, xs)
	
	add-before(|e):
		(x, []) -> [x]
	
	add-before(|e):
		(x, [e|xs]) -> [x, e|xs]
		
	add-before(|e):
		(x, [y|xs]) -> [y|zs]
		where
			!(x, y); not(equal);
			zs := <add-before(|e)> (x, xs)
